<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DEVIntegMessagesLoadSFTP</Name>
	<SourceCode>
		<Declaration><![CDATA[
class DEVIntegMessagesLoadSFTP extends DEVIntegMessagesLoadBaseType
{
    DEVIntegConnectionHelperSFTP            connectionHelper;


}
]]></Declaration>
		<Methods>
			<Method>
				<Name>loadMessageType</Name>
				<Source><![CDATA[
    public int loadMessageType()
    {
        int                 recordCount;
        DEVIntegTmpFiles    fileList;

        ;
        if (! messageTypeTable.ArchiveLocation)
        {
            throw error(strFmt("Message type %1. Archive location is not specified", messageTypeTable.messageTypeId));
        }

        connectionHelper = DEVIntegConnectionHelperSFTP::construct(messageTypeTable);
        connectionHelper.connect();

        fileList = connectionHelper.fillFiles();

        fileList.clear();
        while select fileList
            order by Filename
        {
            this.handleIncomingFile(messageTypeTable, fileList);
            recordCount++;

            if (this.checkMessageReadLimit())
            {
                break;
            }
        }
    
        return recordCount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>handleIncomingFile</Name>
				<Source><![CDATA[
    void handleIncomingFile(DEVIntegMessageTypeInbound   _messageTypeTable, DEVIntegTmpFiles     _fileRef)
    {
        System.IO.MemoryStream  memoryStream;

        memoryStream = connectionHelper.readFile(_fileRef);
        if (memoryStream != null)
        {
            ttsbegin;
            DEVIntegMessageCreator messageCreator = DEVIntegMessageCreator::construct();
            messageCreator.initFromStreamCopy(_fileRef, memoryStream).createMessage(_messageTypeTable);

            connectionHelper.moveFileToDirectory(_fileRef, _messageTypeTable.ArchiveLocation,                 
                _messageTypeTable.FileMoveNameChange == DEVIntegFileMoveNameChange::AppendDateTime ? true : false);
            ttscommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>testOutboundConnection</Name>
				<Source><![CDATA[
    void testOutboundConnection(DEVIntegMessageTypeTableOutbound  _messageTypeTableOutbound)
    {
        connectionHelper =  DEVIntegConnectionHelperSFTP::constructOutbound(_messageTypeTableOutbound);
        
        connectionHelper.testConnection();
    }

]]></Source>
			</Method>
			<Method>
				<Name>testConnection</Name>
				<Source><![CDATA[
    void testConnection()
    {
        DEVIntegConnectionHelperSFTP     azureStorageConnectionHelper =  DEVIntegConnectionHelperSFTP::construct(messageTypeTable);
        azureStorageConnectionHelper.testConnection();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>